# This SPARQL query generates a CSV report of biome terms from the EnvO ontology
# and is intended as training for LLMs performing a subset mapping
# It specifically retrieves information about relationships that involve the biomes
# that might be reasonable answers to the env_braod_scale question form the Minimal Information about any (X) Sequence (MIxS) standard
# If the LLM doesnâ€™t think that the CSV report has been provided, it should ask for it.
# The CSV report may have also been merged with the output from other SPARQL queries.
# The LLM should ask for the other SPARQL queries if it thinks they are missing.
# The CSV report may be provided in chunks.
# If that's the case, then only the first chunk will include the column header.
# The LLM should report the first and last EnvO terms it has received after each cunk is submitted
#
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?s (REPLACE(STR(?s), "http://purl.obolibrary.org/obo/", "") AS ?class) ?sls ?predicate ?o ?ols
WHERE {
    VALUES ?parent {
        <http://purl.obolibrary.org/obo/ENVO_00000428> # biome
    }
    GRAPH <http://reasoner.renci.org/ontology> {
        ?s rdfs:subClassOf+ ?parent ;
        rdfs:isDefinedBy <http://purl.obolibrary.org/obo/envo.owl> .
    }
    GRAPH <http://reasoner.renci.org/redundant> {
        ?s ?p ?o
    }
    GRAPH <http://reasoner.renci.org/ontology> {
        ?o rdfs:isDefinedBy <http://purl.obolibrary.org/obo/envo.owl> .
    }
    OPTIONAL { ?s rdfs:label ?sl . BIND(STR(?sl) AS ?sls) }
    OPTIONAL { ?p rdfs:label ?pl . BIND(REPLACE(STR(?pl), "_", " ") AS ?pls) }
    BIND(IF(BOUND(?pl), ?pls, STR(?p)) AS ?predicate)
    OPTIONAL { ?o rdfs:label ?ol . BIND(STR(?ol) AS ?ols) }
}
