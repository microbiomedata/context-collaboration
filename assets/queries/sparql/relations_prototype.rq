PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?s (REPLACE(STR(?s), "http://purl.obolibrary.org/obo/", "") AS ?class) ?sls ?predicate ?o ?ols
WHERE {
    VALUES ?parent {
        <http://purl.obolibrary.org/obo/BFO_0000024> # fiat object, a material entity
        <http://purl.obolibrary.org/obo/ENVO_00003074> # manufactured product, a material entity
        <http://purl.obolibrary.org/obo/ENVO_00010483> # environmental material, a material entity
        <http://purl.obolibrary.org/obo/ENVO_01000281> # layer, a material entity
        <http://purl.obolibrary.org/obo/ENVO_01000813> # astronomical body part, a material entity
        <http://purl.obolibrary.org/obo/ENVO_01001813> # construction, a material entity
        <http://purl.obolibrary.org/obo/RO_0002577> # system, a material entity
    }
    # add RO:0002577 system ?
    # already included ENVO:00000428 biome, a astronomical body part
    # skipping ENVO:03500005 anthropogenic litter, CHEBI:24431 chemical entity,
    # UBERON:0000465 material anatomical entity, BFO:0000030 object, BFO:0000027 object aggregate,
    # OBI:0100026 organism (with NCBI descendants)
    # PCO:0000031 organismal entity -> collection of organisms
    # PO:0025131 plant anatomical entity -> plant structure, portion of plant substance
    # OBI:0000047 processed material -> device -> container
    # NCBITaxon:1 root
    # CHEBI:36342 subatomic particle
    GRAPH <http://reasoner.renci.org/ontology> {
        ?s rdfs:subClassOf+ ?parent ;
        rdfs:isDefinedBy <http://purl.obolibrary.org/obo/envo.owl> .
    }
    GRAPH <http://reasoner.renci.org/redundant> {
        ?s ?p ?o
    }
    GRAPH <http://reasoner.renci.org/ontology> {
        ?o rdfs:isDefinedBy <http://purl.obolibrary.org/obo/envo.owl> .
    }
    OPTIONAL { ?s rdfs:label ?sl . BIND(STR(?sl) AS ?sls) }
    OPTIONAL { ?p rdfs:label ?pl . BIND(REPLACE(STR(?pl), "_", " ") AS ?pls) }
    BIND(IF(BOUND(?pl), ?pls, STR(?p)) AS ?predicate)
    OPTIONAL { ?o rdfs:label ?ol . BIND(STR(?ol) AS ?ols) }
}
